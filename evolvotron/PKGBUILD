# Maintainer: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=evolvotron
pkgver=0.6.1
pkgrel=5
pkgdesc="An interactive generative art application"
arch=('i686' 'x86_64')
url="http://www.bottlenose.demon.co.uk/share/evolvotron/"
license=('GPL')
depends=('qt')
makedepends=('boost')
source=("http://downloads.sourceforge.net/evolvotron/$pkgname-$pkgver.tar.gz")
md5sums=('88ae3cfdef11e7e7f4859b7847cbcaf6')

build() {
  cd $srcdir/$pkgname

  # Fix two issues:
  # * The configure script calls the qmake-qt4 binary, which does not exist;
  # * Remove the $QTDIR check, qmake is already in $PATH.
  sed -e 's/qmake-qt4/qmake/g;3,11d' -i configure

  ./configure

  make -j1
}

package() {
  cd $srcdir/$pkgname

  for bin in ${pkgname}{,_mutate,_render}; do
    install -D -m 755 $bin/$bin $pkgdir/usr/bin/$bin
  done
  for man in ${pkgname}{,_mutate,_render}.1; do
    install -D -m 644 man/man1/$man $pkgdir/usr/share/man/man1/$man
  done
  install -D -m 644 evolvotron.html $pkgdir/usr/share/doc/$pkgname/manual.html
}
# vim:syntax=sh
